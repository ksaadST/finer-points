import com.moowork.gradle.node.task.NodeTask

buildscript {
    repositories {
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
        classpath("be.filipblondeel.gradle:gradle-gulp-plugin:0.1")
        classpath("com.moowork.gradle:gradle-node-plugin:0.12")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'com.moowork.node'

configurations.all {
    resolutionStrategy {
        // Keeping jackson old for the sake of spring-social-google, bless its heart.
        // Google sign in fails if the version is allowed to move up to 2.7 or 2.7.1.
        force 'com.fasterxml.jackson.core:jackson-core:2.6.5'
        force 'com.fasterxml.jackson.core:jackson-databind:2.6.5'
        force 'com.fasterxml.jackson.core:jackson-annotations:2.6.5'
    }
}

node {
    // Version of node to use. Keep webpack-watch.sh in sync with this.
    version = '4.4.4'
    download = true
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack')
}

task webpackWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack')
    args = ['--watch']
}

// makes sure on each build that gulp is installed
//gulp_build.dependsOn 'installGulp'

// processes package.json before running gulp build
//gulp_build.dependsOn 'npmInstall'

// runs "gulp build" as part of other tasks
//processResources.dependsOn gulp_build

processResources.dependsOn webpack

war {
    baseName = 'FinerPoints'
    version =  '0.1.0'
}

bootRepackage.doLast {
  ant.unzip(src: war.archivePath, dest: "$buildDir/exploded")
}

configurations {
    providedRuntime
}

group = 'tarehart'

jar {
    baseName = 'finer-points'
    version =  '0.1.0'
}

run {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=5005,suspend=n']
}

bootRun {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=5005,suspend=n']
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/libs-release" }
    maven { url "http://m2.neo4j.org" }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url "http://repo.spring.io/libs-snapshot" }
    maven { url "http://m2.neo4j.org/content/repositories/snapshots"}
}

dependencies {
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("io.jsonwebtoken:jjwt:0.6.0")
    compile("org.springframework.social:spring-social-security:1.1.4.RELEASE")
    compile("org.springframework.social:spring-social-google:1.0.0.RELEASE")
    compile("org.springframework:spring-context")
    compile("org.springframework:spring-tx")
    compile("org.springframework.data:spring-data-neo4j:4.1.2.RELEASE") // At this time this is the latest stable version. Feel free to upgrade.
    compile("org.springframework:spring-webmvc")
    compile("joda-time:joda-time:2.8.1")

    testCompile("junit:junit")
    testCompile(group: 'org.neo4j', name: 'neo4j-ogm-test', version: '2.0.1')
    testCompile(group: 'org.neo4j.test', name: 'neo4j-harness', version: '2.3.5')
    testCompile("org.springframework:spring-test")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
